plugins {
    id("maven-publish")
    id("java-library")
}

group("user11681")
version("2.0.0")

sourceSets {
    all {
        extensions.add("classpath") {SourceSet other ->
            runtimeClasspath += other.runtimeClasspath + other.output
            compileClasspath += other.compileClasspath + other.output
        }
    }

    main {
        java.srcDir("src/java8")
    }

    java16 {
        java.srcDir("src/java16")
        classpath(main)
    }

    test {
        java.srcDir("src/test")
        classpath(java16)
    }
}

repositories {
    mavenCentral()

    maven {url = "https://auoeke.jfrog.io/artifactory/maven"}
}

dependencies {
    api("net.gudenau.lib:unsafe:latest.release")

    compileOnly("org.jetbrains:annotations:latest.release")

    testImplementation("org.junit.jupiter:junit-jupiter:latest.release")
    testImplementation("org.ow2.asm:asm:9.0")
    testImplementation("org.ow2.asm:asm-tree:9.0")
    testImplementation("org.openjdk.jol:jol-core:latest.release")
    testImplementation("user11681:uncheck:latest.release")
    testImplementation("net.bytebuddy:byte-buddy-agent:latest.release")
}

java {
    withSourcesJar()
}

compileJava {
    sourceCompatibility = 8
    targetCompatibility = 8
}

compileJava16Java {
    sourceCompatibility = 16
    targetCompatibility = 16
}

jar {
    from(sourceSets.java16.output)
}

publishing {
    publications {
        maven(MavenPublication) {
            from(components.java)
        }
    }

    repositories {
        mavenLocal()

        maven {
            url = "https://auoeke.jfrog.io/artifactory/maven"

            credentials {
                username = System.properties["maven.username"]
                password = System.properties["maven.password"]
            }
        }
    }
}
